/*
 *  Copyright 2022 CNM Ingenuity, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'application'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

group 'edu.cnm.deepdive'

gradlePlugin {
    plugins {
        roomSchemaParserPlugin {
            id = 'edu.cnm.deepdive.room-schema-parser'
            implementationClass = 'edu.cnm.deepdive.room.gradle.Plugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation gradleApi()

    implementation 'com.google.code.gson:gson:2.9.0'

    def junitVersion = '5.8.2'
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

test {
    useJUnitPlatform()
}

ext {
    javaMainClass = 'edu.cnm.deepdive.room.Main'
}

application {
    mainClass = javaMainClass
}

jar {
    exclude('**/.keep')
    manifest {
        attributes(
                'Main-Class': javaMainClass
        )
    }
}

artifacts {
    archives jar
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = "https://maven.pkg.github.com/ddc-java/public-maven-artifacts"
            credentials {
                username = System.getenv('PACKAGE_PUBLISHER_USER')
                password = System.getenv('PACKAGE_PUBLISHER_TOKEN')
            }
        }
    }
    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            artifact jar
            //noinspection GroovyAssignabilityCheck
        }
    }
}
