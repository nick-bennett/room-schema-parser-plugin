/*
 *  Copyright 2022 CNM Ingenuity, Inc.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.0.0-rc-1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

group = 'edu.cnm.deepdive'
version = '1.0.1'

gradlePlugin {
    plugins {
        roomSchemaParserPlugin {
            id = 'edu.cnm.deepdive.room-schema-parser'
            implementationClass = 'edu.cnm.deepdive.room.gradle.Plugin'
            displayName = 'Room Schema Parser'
            description = 'Gradle plugin that can be used to parse & extract the DDL from a schema file generated by Room in an Android project.'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {

    implementation gradleApi()

    implementation 'com.google.code.gson:gson:2.9.0'

    def junitVersion = '5.8.2'
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

test {
    useJUnitPlatform()
}

jar {
    exclude('**/.keep')
}

artifacts {
    archives jar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            pom {
                name = 'Room Schema Parser'
                description = 'Gradle plugin that can be used to parse & extract the DDL from a schema file generated by Room in an Android project.'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'nickbenn'
                        name = 'Nicholas Bennett'
                        email = 'nick@nickbenn.com'
                    }
                }
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/ddc-java/room-schema-parser-plugin'
    vcsUrl = 'https://github.com/ddc-java/room-schema-parser-plugin.git'
    tags = ['android', 'room', 'schema', 'ddl']
}
